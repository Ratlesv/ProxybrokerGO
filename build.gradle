plugins {
    id 'java'
    id 'application'
    // plugin to generate fat jar with all required java libraries
    id 'org.springframework.boot' version '2.5.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    // plugin to provide which dependencies have updates
    // adds dependencyUpdates task
    id 'com.github.ben-manes.versions' version '0.39.0'
    // plugin provides building executable and running application natively without compilation
    // add nativeBuild and nativeRun tasks
    id 'org.graalvm.buildtools.native' version '0.9.5'
    // build docker image ./gradlew jibDockerBuild --image=ziloka/proxybroker
    id 'com.google.cloud.tools.jib' version '3.1.4'
}

sourceCompatibility = '11'

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    // https://stackoverflow.com/a/65731572
    implementation ('info.picocli:picocli-spring-boot-starter:4.6.1'){
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    }
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'com.maxmind.geoip2:geoip2:2.15.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'
    testImplementation 'junit:junit:4.13.2'
}

application {
    // Define the main class for the application.
    mainClass = 'com.ziloka.ProxyBroker.ProxyBroker'
}

bootJar {
    archiveBaseName = 'ProxyBroker'
}

jar {
    archiveBaseName = 'ProxyBroker'
}

graalvmNative.binaries.main {
    imageName = "ProxyBroker"
    mainClass = "com.ziloka.ProxyBroker.ProxyBroker"
    fallback = false;
    verbose = false;
}

jib {
    from.image = 'gcr.io/distroless/java:11'
    container.mainClass = 'com.ziloka.ProxyBroker.ProxyBroker'
}